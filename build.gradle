import org.gradle.api.tasks.testing.logging.TestLogEvent
import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id "org.jetbrains.intellij" version "1.17.3" apply false
}

subprojects {
    apply plugin: 'java'
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { setUrl("https://repo.gradle.org/gradle/repo") }
        maven { setUrl("https://maven.google.com/") }
    }

    apply plugin: 'org.jetbrains.intellij'
    intellij {
        version = project.property('ideaVersion')
        plugins = ['copyright', 'java']
        downloadSources = Boolean.valueOf(project.property('sources') as String)
        sameSinceUntilBuild = Boolean.valueOf(project.property('isEAP') as String)
    }

    tasks.named('patchPluginXml') {
        sinceBuild = project.property('ideaSinceVersion')
        pluginId = "thrift-syntax-fork"
    }

    test {
        useJUnitPlatform()

        testLogging {
            exceptionFormat = TestExceptionFormat.FULL
            events = Set.of(TestLogEvent.PASSED, TestLogEvent.SKIPPED, TestLogEvent.FAILED)
        }
    }

    // Disable search options to improve build time since we don't provide any custom settings
    // See: https://plugins.jetbrains.com/docs/intellij/tools-gradle-intellij-plugin-faq.html#how-to-disable-building-searchable-options
    buildSearchableOptions.enabled = false
}
